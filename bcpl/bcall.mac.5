	TITLE BCALL
;	JJW, 2 MAY 73

;CALL BCPL PROCEDURE FROM FORTRAN ENVIRONMENT.  ALL ARGUMENTS GET
; PASSED TO THE BCPL PROCEDURE BY REFERENCE - I.E., AS ADDRESSES.
;NOTE THAT THERE'S BUT ONE, FIXED LENGTH STACK SPACE, SO FORTRAN CALLING
; BCPL, CALLING FORTRAN, CALLING BCPL WILL LOSE.

;	CALL BCALL(PROC,ARG1,ARG2,...)
;	A=BCALL(PROC,ARG1,ARG2,...)
;	I=IBCALL(PROC,ARG1,ARG2,...)
;WHERE:
; PROC = BCPL PROCEDURE, USUALLY DECLARED IN AN EXTERNAL STATEMENT
; ARG1, ETC = ARGUMENTS TO PROC

Z=0
A=1
B=2
Q=16			;FORTRAN RETURN AC
S=16			;BCPL STACK PTR
ACS:	BLOCK 20	;FOR SAVING AC'S
BSTACK:	BLOCK ^D5000	;LOTSA ROOM FOR BCPL STACK

	ENTRY BCALL,IBCALL

IBCALL:
BCALL:	0
	MOVE Z,[2,,ACS+2]
	BLT Z,ACS+17		;SAVE CALLING AC'S 2-17
	MOVE Z,0(Q)		;GET ADR OF PROCEDURE TO BE CALLED
	HRRM Z,GO
	MOVEI A,BSTACK
	SETZ B,
ARGLP:	HLRZ Z,1(Q)		;GET POTENTIAL ARG
	ANDI Z,(777B8)
	CAIE Z,(JUMP)		;IS IT?
	JRST NOARG		;NO, SO DONE
	MOVE Z,1(Q)		;YES
	HRRZM Z,2(A)		;PUT ARG PTR ON STACK
	AOJ A,
	AOJ Q,
	AOJA B,ARGLP

NOARG:	MOVEM B,BSTACK+1	;NUMBER OF ARGS GO ON STACK
	MOVEI S,BSTACK		;SET BCPL STACK POINTER
	ADDI S,0		;(SO TRACE WILL WORK RIGHT)
GO:	JSP 1,.-.		;CALL BCPL PROCEDURE
	SUBI S,0		;(FOR TRACE...)
	MOVEM 1,ACS+0		;PUT ANY RESULT IN SAVED AC0
	MOVSI 17,ACS		;17 _ ACS,,0
	BLT 17,17		;RESTORE AC'S 0, 2-17
	JRA Q,1(Q)		;RETURN TO FORTRAN CALLER THRU ARG LIST

	END
