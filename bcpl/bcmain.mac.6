TITLE BCMAIN ... BCPL MAIN PROGRAM FOR TENEX

SEARCH STENEX

S=16	; THE BCPL STACK POINTER

EXTERN	.JBFF	;STACK START
EXTERN	GL271	;STATIC IN BCPSYS FOR STACK START
EXTERN	GL270	;STATIC IN BCPSYS FOR SAFE17 BOOLEAN

BCMAIN:
	JRST	E0
	JRST	E1
	JRST	E2
	0
	0
	;SAVE THE INITIAL ACS IN GL2
SVACS:	MOVEM	17,I.ACS+17
	MOVEI	17,I.ACS
	BLT	17,I.ACS+16
	RESET

	; SET ^A, ^Q, ^R TO NOT ECHO
	MOVEI	1,101
	RFCOC
	AND	2,[637777777774]
	AND	3,[177777777777]
	SFCOC

	; SET THE STAK POINTER, AND REMEMBER IT'S INITIAL VALUE
	HRRZ	S,.JBFF	;STACK PTR
	ADDI	S,10
	MOVEM	S,I.SBASE
	MOVEM	S,GL271
	MOVEI	17,3(S)
	SETOM	GL270

	; { START() ; FINISH} REPEAT
	MOVE	1,NTRYNM
	MOVEM	1,2(S)
	MOVEI	1,1
	MOVEM	1,1(S)	; ONE PARAMETER TO THE START ROUTINE:
			;     ENTRY #, FROM 0
	ADDI	S,0
	JSP	1,GL1	;CALL START
	SUBI	S,0
	MOVEI	17,3(S)
	HALTF
	JRST	.-1

	;REMEMBER THAT ENTRY #0 OCCURRED
E0:	MOVEM	1,I.ACS+1
	MOVEI	1,0
	MOVEM	1,NTRYNM
	MOVE	1,I.ACS+1
	JRST	SVACS
	;REMEMBER THAT ENTRY #1 OCCURRED
E1:	MOVEM	1,I.ACS+1
	MOVEI	1,1
	MOVEM	1,NTRYNM
	MOVE	1,I.ACS+1
	JRST	SVACS
	;REMEMBER THAT ENTRY #2 OCCURRED
E2:	MOVEM	1,I.ACS+1
	MOVEI	1,2
	MOVEM	1,NTRYNM
	MOVE	1,I.ACS+1
	JRST	SVACS

I.ACS:	BLOCK	20		;INITIAL AC'S
I.SBASE: 0		;STACK BASE
NTRYNM:	0

BCFRST:	0
GL2:	I.ACS
EXTERN	GL1

ENTRY	GL2,BCFRST

END	BCMAIN
