

// llread.b
// a program to move a text file from Lincoln TX-2 to BBN-TENEX
// Rovner, 1/13/73
//

get "<BCPL>HEAD"
get "<BCPL>NLHEAD"
get "<BCPL>UTILHEAD"

static{ DBGSW_false}

let Start() be
 { InitNetLibrary()
   OUTPUT_CreateOutput(0)	//primary output stream
   let ttyin_FindInput(0)	//tty input stream

   { WriteS("debug? Y or N:")
   let tt_BIN(ttyin)
   WriteS("*n")
   if tt=$Y \ tt=$y do { DBGSW_true ; break}
if tt=$N \ tt=$n do break} repeat
   let callvec_vec 20
   let tx2out,tx2in_0,0
   { let t_ CreateNetDialogue(-1,,"LL-TX-2",lv tx2out,lv tx2in)
	if t=0 break
	if DBGSW do { WriteS("CND=#") ; WriteOct(t) ; PBOUT($*n)}
     WriteS("Still trying to connect to Lincoln*n")
     Wait(8000)	//8 second Wait
   } repeat

   INPUT_tx2in	//primary input stream
   until LLPBIN()=$*s & LLPBIN()=$I & LLPBIN()=$N & LLPBIN()=$*s do
    loop

   WriteS("tty connection to TX-2 established*n")
   LLWriteS(tx2out,"bbn*c*l")
   until LLPBIN()=$W & LLPBIN()=$W & LLPBIN()=$W & LLPBIN()=$W do
      loop
   LLWriteS(tx2out,"bush*c*l")

   until LLPBIN()=$*c & LLPBIN()=$*l & LLPBIN()=$^[ & LLPBIN()=$2 & LLPBIN()=$*s do
      loop
   let inf_CreateOutput(1)
   let outfilename_vec 30
   WriteS("TX-2 file name:*s")
   ReadWord(ttyin,outfilename)
   LLWriteS(tx2out,"5type ")
   LLWriteS(tx2out,outfilename)
   LLWriteS(tx2out,"*c*l")
{ let ch_LLPBIN()
  unless ch=$^[ do
   { Writech(inf,ch)
     loop
   }
  ch_LLPBIN()
  unless ch=$2 do
   { Writech(inf,$^[)
     Writech(inf,ch)
     loop
   }
  ch_LLPBIN()
  unless ch=$*s do
   { Writech(inf,$^[)
     Writech(inf,$2)
     Writech(inf,ch)
     loop
   }
  break
 } repeat
EndWrite(inf)
   LLWriteS(tx2out,"qq*c*l")
   until LLPBIN()=$o & LLPBIN()=$w & LLPBIN()=$? &LLPBIN()=$*c & LLPBIN()=$*l do loop
   LLWriteS(tx2out,"ab*c*l")
   until LLPBIN()=$*s & LLPBIN()=$I & LLPBIN()=$N & LLPBIN()=$*s do loop
   EndNetDialogue(tx2out,tx2in)
 }

and LLPBIN() _ valof
 { let ch_PBIN()
   if DBGSW do Writech(OUTPUT,ch)
   resultis ch
 }


and SendRFC(nets) be
 { let v_vec 20
   v|1_nets
   v|2_#20
   JSYS(#77,v)	//MTOPR
 }


and LLWriteS(outs,strng) be
 { WriteS(outs,strng)
   if DBGSW do WriteS(strng)
 }

and LLWritech(outs,ch) be
 { Writech(outs,ch)
   if DBGSW do Writech(OUTPUT,ch)
 }


